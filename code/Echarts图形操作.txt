.版本 2

.程序集 Echarts图形操作, , 公开

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用



.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用



.子程序 显示加载loding动画, 文本型, 公开, 【echartsInstance.showLoading()】，返回js代码。loding动画不阻塞图形绘制。
.参数 loading标题文字, 文本型, 可空, 默认显示“loading”，也可以自行指定，例如：“一大波数据正在狂奔而来，请稍候...”
.参数 loading标题文字颜色, 文本型, 可空, 默认“#000”
.参数 loading图标颜色, 文本型, 可空, 默认“#c23531”
.参数 loading遮罩颜色, 文本型, 可空, 默认“rgba(255,255,255,0.8)”，0.8为透明度，透明度范围0-1。
.参数 图形对象名称, 文本型, 可空, 单图形时可省略，会自动获取。
.局部变量 loading, eCharts_JSON类

loading.解析 ()
.如果真 (是否为空 (loading标题文字) ＝ 假)
    loading.置新值 (“text”, , loading标题文字)
.如果真结束
.如果真 (是否为空 (loading标题文字颜色) ＝ 假)
    loading.置新值 (“textColor”, , loading标题文字颜色)
.如果真结束
.如果真 (是否为空 (loading图标颜色) ＝ 假)
    loading.置新值 (“color”, , loading图标颜色)
.如果真结束
.如果真 (是否为空 (loading遮罩颜色) ＝ 假)
    loading.置新值 (“maskColor”, , loading遮罩颜色)
.如果真结束
.如果 (是否为空 (图形对象名称))
    返回 (全局_当前图形对象属性.名称 ＋ “.showLoading(” ＋ loading.取代码 () ＋ “)”)
.否则
    返回 (图形对象名称 ＋ “.showLoading(” ＋ loading.取代码 () ＋ “)”)
.如果结束


.子程序 关闭loading动画, 文本型, 公开, 【echartsInstance.hideLoading()】，返回js代码
.参数 图形对象名称, 文本型, 可空, 单图形时可省略，会自动获取

.如果 (是否为空 (图形对象名称))
    返回 (全局_当前图形对象属性.名称 ＋ “.hideLoading()”)
.否则
    返回 (图形对象名称 ＋ “.hideLoading()”)
.如果结束


.子程序 重置图表尺寸, 文本型, 公开, 【echartsInstance.resize()】，返回js代码
.参数 新宽度, 双精度小数型, 可空, 单位px（像素），留空则保留原宽度。
.参数 新高度, 双精度小数型, 可空, 单位px（像素），留空则保留原高度。
.参数 图形对象名称, 文本型, 可空, 单图形时可省略，会自动获取。
.参数 图形对象divID, 文本型, 可空, 单图形时可省略，会自动获取。
.局部变量 JS代码, 文本型

JS代码 ＝ #常用JS代码_设置指定图形尺寸
.如果 (是否为空 (图形对象divID))
    JS代码 ＝ 子文本替换 (JS代码, “%图形DIV名称%”, 全局_当前图形对象属性.divID, , , 真)
.否则
    JS代码 ＝ 子文本替换 (JS代码, “%图形DIV名称%”, 图形对象divID, , , 真)
.如果结束
JS代码 ＝ 子文本替换 (JS代码, “%标签宽度%”, 到文本 (新宽度) ＋ “px”, , , 真)
JS代码 ＝ 子文本替换 (JS代码, “%标签高度%”, 到文本 (新高度) ＋ “px”, , , 真)
.如果 (是否为空 (图形对象名称))
    JS代码 ＝ 子文本替换 (JS代码, “%图形对象名称%”, 全局_当前图形对象属性.名称, , , 真)
.否则
    JS代码 ＝ 子文本替换 (JS代码, “%图形对象名称%”, 图形对象名称, , , 真)
.如果结束
返回 (JS代码)


.子程序 触发图表行为, 文本型, 公开, 【echartsInstance.dispatchAction()】，返回js代码。例如可触发图例开关legendToggleSelect, 数据区域缩放dataZoom，显示提示框showTip等等
.参数 图表行为json代码, 文本型, , 参考：http://echarts.baidu.com/api.html#echartsInstance.dispatchAction
.参数 图形对象名称, 文本型, 可空, 单图形时可省略，会自动获取。

.如果 (是否为空 (图形对象名称))
    返回 (全局_当前图形对象属性.名称 ＋ “.dispatchAction(” ＋ 图表行为json代码 ＋ “)”)
.否则
    返回 (图形对象名称 ＋ “.dispatchAction(” ＋ 图表行为json代码 ＋ “)”)
.如果结束


.子程序 绑定事件处理函数, 文本型, 公开, 【echartsInstance.on()】，返回js代码。
.参数 事件类型, 文本型, ,  ' 1.click（单击）' 2.dblclick（双击）' 3.mousedown（鼠标按下）' 4.mouseup（鼠标放开）' 5.mouseover（鼠标移入）' 6.mouseout（鼠标移出）' 7.globalout（全局移出）' 8.contextmenu（鼠标右键单击）
.参数 js事件处理代码, 文本型, , 事件触发时要运行的js代码。
.参数 图形对象名称, 文本型, 可空, 单图形时可省略，会自动获取。

.如果 (是否为空 (图形对象名称))
    返回 (全局_当前图形对象属性.名称 ＋ “.on('” ＋ 事件类型 ＋ “',” ＋ js事件处理代码 ＋ “)”)
.否则
    返回 (图形对象名称 ＋ “.on('” ＋ 事件类型 ＋ “',” ＋ js事件处理代码 ＋ “)”)
.如果结束


.子程序 解绑事件处理函数, 文本型, 公开, 【echartsInstance.off()】，返回js代码
.参数 事件类型, 文本型, ,  ' 1.click（单击）' 2.dblclick（双击）' 3.mousedown（鼠标按下）' 4.mouseup（鼠标放开）' 5.mouseover（鼠标移入）' 6.mouseout（鼠标移出）' 7.globalout（全局移出）' 8.contextmenu（鼠标右键单击）
.参数 图形对象名称, 文本型, 可空, 单图形时可省略，会自动获取。

.如果 (是否为空 (图形对象名称))
    返回 (全局_当前图形对象属性.名称 ＋ “.off('” ＋ 事件类型 ＋ “')”)
.否则
    返回 (图形对象名称 ＋ “.off('” ＋ 事件类型 ＋ “')”)
.如果结束


.子程序 导出图表图片, 文本型, 公开, 【echartsInstance.getDataURL()】，返回js代码。本代码执行完毕将返回一个base64格式的图片数据，需借助浏览框控件读取返回的图片数据。
.参数 存储导出数据的js变量, 文本型, , 模块无法直接与浏览器通讯，所以只能保存到浏览器的js变量中，再自行使用浏览器的命令取回js变量中的数据。
.参数 导出图片的格式, 整数型, , 1=png，2=jpeg
.参数 导出图片背景色, 文本型, 可空, 黑色主题的图形导出时这里需手动设置黑色背景。
.参数 图形对象名称, 文本型, 可空, 单图形时可省略，会自动获取。
.局部变量 json, eCharts_JSON类

json.解析 ()
.判断开始 (导出图片的格式 ＝ 1)
    json.置新值 (“type”, , “png”)
.判断 (导出图片的格式 ＝ 2)
    json.置新值 (“type”, , “jpeg”)
.默认

.判断结束

.如果真 (是否为空 (导出图片背景色) ＝ 假)
    json.置新值 (“backgroundColor”, , 导出图片背景色)
.如果真结束
.如果 (是否为空 (图形对象名称))
    返回 (存储导出数据的js变量 ＋ “ = ” ＋ 全局_当前图形对象属性.名称 ＋ “.getDataURL(” ＋ json.取代码 () ＋ “)”)
.否则
    返回 (存储导出数据的js变量 ＋ “ = ” ＋ 图形对象名称 ＋ “.getDataURL(” ＋ json.取代码 () ＋ “)”)
.如果结束


.子程序 清空当前图形实例, 文本型, 公开, 【echartsInstance.clear()】，返回js代码。
.参数 图形对象名称, 文本型, 可空, 单图形时可省略，会自动获取。

.如果 (是否为空 (图形对象名称))
    返回 (全局_当前图形对象属性.名称 ＋ “.clear()”)
.否则
    返回 (图形对象名称 ＋ “.clear()”)
.如果结束


.子程序 变量赋值, 文本型, 公开, var
.参数 JS变量名, 文本型
.参数 变量值, 文本型

返回 (JS变量名 ＋ “ = ” ＋ 变量值)


.子程序 股票K线_计算MA均线, 文本型, 公开, 使用js引擎计算ma均线，大量数据时推荐使用。
.参数 保存K线数据的JS变量名, 文本型, , 需提前将K线数据传入js引擎，并指定了js变量名。
.参数 要计算的MA天数, 整数型
.参数 保存计算结果的js变量名, 文本型, , 计算结果将保存至js引擎，使用js变量名调用。
.局部变量 js代码, 文本型

js代码 ＝ #常用JS代码_K线图_计算MA
js代码 ＝ 子文本替换 (js代码, “%data_k%”, 保存K线数据的JS变量名, , , 真)
js代码 ＝ 子文本替换 (js代码, “%ma_day%”, 到文本 (要计算的MA天数), , , 真)
js代码 ＝ 子文本替换 (js代码, “%var_ma%”, 保存计算结果的js变量名, , , 真)
返回 (js代码)

.子程序 注册地图, 文本型, 公开, 【echarts.registerMap】，返回js代码。
.参数 地图名称, 文本型, , 【mapName】，名称用于在geo地理坐标系或地图数据中调用。
.参数 地图geoJSON, 文本型, , 【geoJson】
.局部变量 js代码, 文本型
.局部变量 变量名, 文本型

变量名 ＝ “_mapData” ＋ 到文本 (取随机数 (100, 999))
js代码 ＝ “var ” ＋ 变量名 ＋ “=” ＋ 地图geoJSON ＋ “;echarts.registerMap('” ＋ 地图名称 ＋ “',” ＋ 变量名 ＋ “);”
返回 (js代码)
