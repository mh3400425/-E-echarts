.版本 2

.程序集 STStringUtil

.子程序 StrToArray, 整数型
.参数 string, 文本型
.参数 flag, 文本型, 参考
.参数 arr, 文本型, 参考 数组
.局部变量 i, 整数型
.局部变量 len, 整数型

string ＝ 删首尾空 (string)
重定义数组 (arr, 假, 0)
.如果真 (寻找文本 (string, flag, , 假) ＝ -1)
    加入成员 (arr, string)
    返回 (1)
.如果真结束

arr ＝ 分割文本 (string, flag, )
len ＝ 取数组成员数 (arr)
.变量循环首 (len, 1, -1, i)
    arr [i] ＝ 删首尾空 (arr [i])
    .如果真 (取文本长度 (arr [i]) ＝ 0)
        删除成员 (arr, i, 1)
    .如果真结束

.变量循环尾 ()
返回 (取数组成员数 (arr))


.子程序 ArrayToStr, 文本型
.参数 arr, 文本型, 数组
.参数 flag, 文本型, 参考
.局部变量 str, 文本型
.局部变量 len, 整数型
.局部变量 i, 整数型

len ＝ 取数组成员数 (arr)
.计次循环首 (len, i)
    arr [i] ＝ 删首尾空 (arr [i])
    .如果真 (取文本长度 (arr [i]) ＞ 0)
        .如果真 (取文本长度 (str) ＞ 0)
            str ＝ str ＋ flag
        .如果真结束
        str ＝ str ＋ arr [i]
    .如果真结束

.计次循环尾 ()
返回 (str)


.子程序 SortStringArray
.参数 array, 文本型, 参考 数组
.参数 desc, 逻辑型, 可空, 是否为反向排序(默认为 false)
.局部变量 i
.局部变量 j
.局部变量 len
.局部变量 str, 文本型
.局部变量 sort, 逻辑型

.如果真 (是否为空 (desc))
    desc ＝ 假
.如果真结束

len ＝ 取数组成员数 (array)
.如果真 (len ＝ 0)
    返回 ()
.如果真结束
.计次循环首 (len, j)
    .计次循环首 (len － 1, i)
        sort ＝ 假
        .如果 (desc)
            sort ＝ 文本比较 (array [i], array [i ＋ 1], 真) ＜ 0
        .否则
            sort ＝ 文本比较 (array [i], array [i ＋ 1], 真) ＞ 0
        .如果结束
        .如果真 (sort)
            str ＝ array [i ＋ 1]
            array [i ＋ 1] ＝ array [i]
            array [i] ＝ str
        .如果真结束

    .计次循环尾 ()
.计次循环尾 ()

