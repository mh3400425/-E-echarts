.版本 2

.程序集 快速生成图形

.子程序 开启或关闭文字标签, , 公开, 开启或者关闭文字标签，适用于：折线、饼图、柱形图等
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 开启还是关闭, 逻辑型, 可空, 为空则翻转状态，开始改关闭，关闭改开始。
.参数 指定某条折线, 整数型, 可空, 位置从0开始，为空时操作全部折线。
.局部变量 计数器, 整数型

.如果 (是否为空 (指定某条折线))
    .计次循环首 (json.数组_取长度 (“series”), 计数器)
        .如果 (是否为空 (开启还是关闭))
            json.置新值 (“series[” ＋ 到文本 (计数器 － 1) ＋ “].label.normal.show”, , , , 取反 (json.取逻辑 (“series[” ＋ 到文本 (计数器 － 1) ＋ “].label.normal.show”)))
        .否则
            json.置新值 (“series[” ＋ 到文本 (计数器 － 1) ＋ “].label.normal.show”, , , , 开启还是关闭)
        .如果结束

    .计次循环尾 ()
.否则
    .如果 (是否为空 (开启还是关闭))
        json.置新值 (“series[” ＋ 到文本 (指定某条折线) ＋ “].label.normal.show”, , , , 取反 (json.取逻辑 (“series[” ＋ 到文本 (指定某条折线) ＋ “].label.normal.show”)))
    .否则
        json.置新值 (“series[” ＋ 到文本 (指定某条折线) ＋ “].label.normal.show”, , , , 开启还是关闭)
    .如果结束

.如果结束


.子程序 设置图例组件, , 公开, 设置图例组件，参数留空将保持原设定。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 开启还是关闭, 逻辑型, 可空, 为空保持原状态，设真为开启，设假为关闭。
.参数 图例数据, 文本型, 可空, 为空时将自动获取（必须先设置图形数据才能自动获取）。图例数据组。格式为：“['数据1','数据2'...]”，强烈建议由“创建JSON数组()”生成。
.参数 图例布局朝向, 逻辑型, 可空, 默认为：假=水平排列，设真=垂直排列，设假=水平排列。
.参数 左, 文本型, 可空, 默认为“auto”，组件距离容器左侧的距离。可选“left”、“center”、“right”或百分比“20%”或像素值“20px”。
.参数 右, 文本型, 可空, 默认为“auto”，组件距离容器上侧的距离。可选“top”、“middle”、“bottom”或百分比“20%”或像素值“20px”。
.参数 上, 文本型, 可空, 默认为“auto”，组件距离容器右侧的距离。可选百分比“20%”或像素值“20px”。
.参数 下, 文本型, 可空, 默认为“auto”，组件距离容器下侧的距离。可选百分比“20%”或像素值“20px”。
.局部变量 计数器, 整数型
.局部变量 计数器1, 整数型
.局部变量 计数器2, 整数型
.局部变量 名称组, 文本型, , "0"

.如果 (json.数组_取长度 (“legend”) ≥ 1)
    .计次循环首 (json.数组_取长度 (“legend”), 计数器)
        .如果真 (是否为空 (开启还是关闭) ＝ 假)
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].show”, , , , 开启还是关闭)
        .如果真结束
        .如果真 (是否为空 (图例布局朝向) ＝ 假)
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].orient”, , , , 图例布局朝向)
        .如果真结束
        .如果 (是否为空 (图例数据))
            清除数组 (名称组)
            .计次循环首 (json.数组_取长度 (“series”), 计数器1)
                .判断开始 (json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].type”) ＝ “graph”)
                    .计次循环首 (json.数组_取长度 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].categories”), 计数器2)
                        加入成员 (名称组, json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].categories[” ＋ 到文本 (计数器2 － 1) ＋ “].name”))
                    .计次循环尾 ()
                .判断 (json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].type”) ＝ “radar”)
                    .计次循环首 (json.数组_取长度 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].data”), 计数器2)
                        加入成员 (名称组, json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].data[” ＋ 到文本 (计数器2 － 1) ＋ “].name”))
                    .计次循环尾 ()
                .默认
                    加入成员 (名称组, json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].name”))

                .判断结束

            .计次循环尾 ()
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].data”, 创建JSON数组 (名称组))
        .否则
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].data”, 图例数据)
        .如果结束
        .如果 (是否为空 (左))
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].left”, , “auto”)
        .否则
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].left”, , 左)
        .如果结束
        .如果 (是否为空 (右))
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].right”, , “auto”)
        .否则
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].right”, , 右)
        .如果结束
        .如果 (是否为空 (上))
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].top”, , “auto”)
        .否则
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].top”, , 上)
        .如果结束
        .如果 (是否为空 (下))
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].bottom”, , “auto”)
        .否则
            json.置新值 (“legend[” ＋ 到文本 (计数器 － 1) ＋ “].bottom”, , 下)
        .如果结束

    .计次循环尾 ()
.否则
    .如果真 (是否为空 (开启还是关闭) ＝ 假)
        json.置新值 (“legend.show”, , , , 开启还是关闭)
    .如果真结束
    .如果真 (是否为空 (图例布局朝向) ＝ 假)
        json.置新值 (“legend.orient”, , , , 图例布局朝向)
    .如果真结束
    .如果 (是否为空 (图例数据))
        清除数组 (名称组)
        .计次循环首 (json.数组_取长度 (“series”), 计数器1)
            .判断开始 (json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].type”) ＝ “graph”)
                .计次循环首 (json.数组_取长度 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].categories”), 计数器2)
                    加入成员 (名称组, json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].categories[” ＋ 到文本 (计数器2 － 1) ＋ “].name”))
                .计次循环尾 ()
            .判断 (json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].type”) ＝ “radar”)
                .计次循环首 (json.数组_取长度 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].data”), 计数器2)
                    加入成员 (名称组, json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].data[” ＋ 到文本 (计数器2 － 1) ＋ “].name”))
                .计次循环尾 ()
            .默认
                加入成员 (名称组, json.取文本 (“series[” ＋ 到文本 (计数器1 － 1) ＋ “].name”))
            .判断结束

        .计次循环尾 ()
        json.置新值 (“legend.data”, 创建JSON数组 (名称组))
    .否则
        json.置新值 (“legend.data”, 图例数据)
    .如果结束
    .如果 (是否为空 (左))
        json.置新值 (“legend.left”, , “auto”)
    .否则
        json.置新值 (“legend.left”, , 左)
    .如果结束
    .如果 (是否为空 (右))
        json.置新值 (“legend.right”, , “auto”)
    .否则
        json.置新值 (“legend.right”, , 右)
    .如果结束
    .如果 (是否为空 (上))
        json.置新值 (“legend.top”, , “auto”)
    .否则
        json.置新值 (“legend.top”, , 上)
    .如果结束
    .如果 (是否为空 (下))
        json.置新值 (“legend.bottom”, , “auto”)
    .否则
        json.置新值 (“legend.bottom”, , 下)
    .如果结束

.如果结束


.子程序 设置标题组件, , 公开, 设置标题组件，参数留空将保持原设定。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 开启还是关闭, 逻辑型, 可空, 为空保持原状态，设真为开启，设假为关闭。
.参数 新标题, 文本型, 可空
.参数 新副标题, 文本型, 可空
.参数 背景颜色, 文本型, 可空, 默认为：'transparent'透明无色，也可设置为颜色值如：“#FFF”或“#FFFFFF”
.参数 主标题_文字颜色, 文本型, 可空, 默认为：“#333”
.参数 主标题_文字大小, 整数型, 可空, 默认为：18
.参数 副标题_文字颜色, 文本型, 可空, 默认为：“#aaa”
.参数 副标题_文字大小, 整数型, 可空, 默认为：12
.参数 左, 文本型, 可空, 默认为“auto”，组件距离容器左侧的距离。可选“left”、“center”、“right”或百分比“20%”或像素值“20px”。
.参数 右, 文本型, 可空, 默认为“auto”，组件距离容器上侧的距离。可选“top”、“middle”、“bottom”或百分比“20%”或像素值“20px”。
.参数 上, 文本型, 可空, 默认为“auto”，组件距离容器右侧的距离。可选百分比“20%”或像素值“20px”。
.参数 下, 文本型, 可空, 默认为“auto”，组件距离容器下侧的距离。可选百分比“20%”或像素值“20px”。
.局部变量 计数器, 整数型

.如果真 (是否为空 (开启还是关闭) ＝ 假)
    json.置新值 (“title.show”, , , , 开启还是关闭)
.如果真结束
.如果真 (是否为空 (新标题) ＝ 假)
    json.置新值 (“title.text”, , 新标题)
.如果真结束
.如果真 (是否为空 (新副标题) ＝ 假)
    json.置新值 (“title.subtext”, , 新副标题)
.如果真结束
.如果真 (是否为空 (背景颜色) ＝ 假)
    json.置新值 (“title.backgroundColor”, , 背景颜色)
.如果真结束
.如果真 (是否为空 (主标题_文字颜色) ＝ 假)
    json.置新值 (“title.textStyle.color”, , 主标题_文字颜色)
.如果真结束
.如果真 (是否为空 (主标题_文字大小) ＝ 假)
    json.置新值 (“title.textStyle.fontSize”, , , 主标题_文字大小)
.如果真结束
.如果真 (是否为空 (副标题_文字颜色) ＝ 假)
    json.置新值 (“title.subtextStyle.color”, , 副标题_文字颜色)
.如果真结束
.如果真 (是否为空 (副标题_文字大小) ＝ 假)
    json.置新值 (“title.subtextStyle.fontSize”, , , 副标题_文字大小)
.如果真结束
.如果 (是否为空 (左))
    json.置新值 (“title.left”, , “auto”)
.否则
    json.置新值 (“title.left”, , 左)
.如果结束
.如果 (是否为空 (右))
    json.置新值 (“title.right”, , “auto”)
.否则
    json.置新值 (“title.right”, , 右)
.如果结束
.如果 (是否为空 (上))
    json.置新值 (“title.top”, , “auto”)
.否则
    json.置新值 (“title.top”, , 上)
.如果结束
.如果 (是否为空 (下))
    json.置新值 (“title.bottom”, , “auto”)
.否则
    json.置新值 (“title.bottom”, , 下)
.如果结束


.子程序 设置X轴, 整数型, 公开, 设置X轴组件，返回此x轴的索引号，支持设置或修改多组X/Y轴组件，参数留空将保持原设定。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 开启还是关闭, 逻辑型, 可空, 默认为开启，设真为开启，设假为关闭。
.参数 X轴数据, 文本型, 可空, X轴数据组。格式为：“['一月','二月'...]”，强烈建议由“创建JSON数组()”生成。
.参数 位置, 逻辑型, 可空, 默认为：假=底部。为真=顶部，为假=底部。
.参数 是否两侧留空, 逻辑型, 可空, 是否两侧留空。默认为真，为真留空，为假不留空。
.参数 刻度最大值, 文本型, 可空, 默认为自动识别，可指定为具体数值（需转换为文本型），或者可指定为“dataMax”将自动取数据在该轴上最大值。
.参数 刻度最小值, 文本型, 可空, 默认为自动识别，可指定为具体数值（需转换为文本型），或者可指定为“dataMin”将自动取数据在该轴上最小值。
.参数 刻度分段数, 整数型, 可空, 默认为：5
.参数 坐标轴类型, 文本型, 可空, 默认为：类目轴“category”。可选常量：#坐标轴类型_**轴。
.参数 名称, 文本型, 可空, 默认为空。
.参数 格式, 文本型, 可空, 为空时默认显示原始数值，格式支持字符串模板。例如：“第 {value} 个”。
.参数 刻度标签旋转角度, 整数型, 可空, 默认不旋转，角度值：-90~90。用于类目标签不够位置显示时候通过旋转防止标签直接重叠。
.参数 间隔显示标签, 整数型, 可空, 留空为自动显示标签。默认echarts会使用标签不重叠的策略自动决定显示标签。设置0会强制显示所有标签，设置x（x大于等于1）的数字会间隔x来显示标签。
.参数 轴颜色, 文本型, 可空
.参数 绑定grid组件, 整数型, 可空, 默认绑定索引0的grid（grid组件不设置将会自动创建0号grid组件）。
.参数 修改指定索引的x轴参数, 整数型, 可空, 索引从0开始。留空或指定的索引不存在时将添加新x轴，指定的索引存在将覆盖其参数。
.局部变量 计数器, 整数型
.局部变量 索引, 整数型

.如果 (是否为空 (修改指定索引的x轴参数) 或 修改指定索引的x轴参数 ≤ json.数组_取长度 (“xAxis”) － 1)  ' 指定的索引留空或者不存在时将创建新x轴，否则将修改指定索引的x轴参数
    索引 ＝ json.数组_加入成员 (“xAxis”, “{}”) － 1
.否则
    索引 ＝ 修改指定索引的x轴参数
.如果结束

' 开启还是关闭
.如果 (是否为空 (开启还是关闭))
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].show”, , , , 真)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].show”, , , , 开启还是关闭)
.如果结束
' X轴数据
.如果 (是否为空 (X轴数据))
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].data”, “”)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].data”, X轴数据)
.如果结束
' 位置
.如果 (是否为空 (位置))
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].position”, , “bottom”)
.否则
    .如果 (位置)
        json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].position”, , “top”)
    .否则
        json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].position”, , “bottom”)
    .如果结束

.如果结束
' 是否两侧留空
.如果 (是否为空 (是否两侧留空))
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].boundaryGap”, , , , 真)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].boundaryGap”, , , , 是否两侧留空)
.如果结束
' 刻度最大值
.如果 (是否为空 (刻度最大值))
    json.删除 (“xAxis[” ＋ 到文本 (索引) ＋ “].max”)
.否则
    .如果 (刻度最大值 ＝ “dataMax”)
        json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].max”, , “dataMax”)
    .否则
        json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].max”, , , 到数值 (刻度最大值))
    .如果结束

.如果结束
' 刻度最小值
.如果 (是否为空 (刻度最小值))
    json.删除 (“xAxis[” ＋ 到文本 (索引) ＋ “].min”)
.否则
    .如果 (刻度最小值 ＝ “dataMin”)
        json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].min”, , “dataMin”)
    .否则
        json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].min”, , , 到数值 (刻度最小值))
    .如果结束

.如果结束
' 刻度分段数
.如果 (是否为空 (刻度分段数))
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].splitNumber”, , , 5)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].splitNumber”, , , 刻度分段数)
.如果结束
' 坐标轴类型
.如果 (是否为空 (坐标轴类型))
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].type”, , “category”)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].type”, , 坐标轴类型)
.如果结束
' 名称
.如果 (是否为空 (名称))
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].name”, , “”)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].name”, , 名称)
.如果结束
' 格式
.如果 (是否为空 (格式))
    json.删除 (“xAxis[” ＋ 到文本 (索引) ＋ “].axisLabel.formatter”)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].axisLabel.formatter”, , 格式)
.如果结束
' 刻度标签旋转角度
.如果 (是否为空 (刻度标签旋转角度))
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].axisLabel.rotate”, , , 0)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].axisLabel.rotate”, , , 刻度标签旋转角度)
.如果结束
' 间隔显示标签
.如果 (是否为空 (间隔显示标签))
    json.删除 (“xAxis[” ＋ 到文本 (索引) ＋ “].axisLabel.interval”)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].axisLabel.interval”, , , 间隔显示标签)
.如果结束
' 轴颜色
.如果 (是否为空 (轴颜色))
    json.删除 (“xAxis[” ＋ 到文本 (索引) ＋ “].axisLine.lineStyle.color”)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].axisLine.lineStyle.color”, , 轴颜色)
.如果结束
' 绑定GRID组件
.如果 (是否为空 (绑定grid组件))
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].gridIndex”, , , 0)
.否则
    json.置新值 (“xAxis[” ＋ 到文本 (索引) ＋ “].gridIndex”, , , 绑定grid组件)
.如果结束

返回 (索引)

.子程序 设置Y轴, 整数型, 公开, 设置Y轴组件，返回此y轴的索引号，支持设置或修改多组X/Y轴组件，参数留空将保持原设定。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 开启还是关闭, 逻辑型, 可空, 默认为开启，真为开启，假为关闭。
.参数 位置, 逻辑型, 可空, 默认为：假=左边。为真=右边，为假=左边。
.参数 是否脱离锁定0刻度, 逻辑型, 可空, 默认为：假=永远显示0刻度。为真=脱离0刻度锁定（相当于设置了dataMax和dataMin但会自动在图形数据区外添加视觉缓冲区），设置"dataMax"或"dataMin"后此参数会失效。
.参数 刻度最大值, 文本型, 可空, 默认为自动识别，可指定为具体数值（需转换为文本型），或者可指定为“dataMax”将自动取数据在该轴上最大值。
.参数 刻度最小值, 文本型, 可空, 默认为自动识别，可指定为具体数值（需转换为文本型），或者可指定为“dataMin”将自动取数据在该轴上最小值。
.参数 刻度分段数, 整数型, 可空, 默认为：5
.参数 坐标轴类型, 文本型, 可空, 默认为：数值轴“value”。可选常量：#坐标轴类型_**轴。
.参数 名称, 文本型, 可空, 默认为空。
.参数 格式, 文本型, 可空, 为空时默认显示原始数值，格式支持字符串模板。例如：“第 {value} 个”。
.参数 是否锁定0刻度, 逻辑型, 可空, 默认为假绑定0刻度。真=不绑定0刻度，效果相当于max或min参数设置了dataMax与dataMin（已设置了max或min后此参数将失效）。
.参数 反向坐标轴, 逻辑型, 可空, 默认为假（不反向）。
.参数 轴颜色, 文本型, 可空
.参数 绑定grid组件, 整数型, 可空, 默认绑定索引0的grid（grid组件不设置将会自动创建0号grid组件）。
.参数 修改指定索引的x轴参数, 整数型, 可空, 索引从0开始。留空或指定的索引不存在时将添加新x轴，指定的索引存在将覆盖其参数。
.局部变量 计数器, 整数型
.局部变量 索引, 整数型

.如果 (是否为空 (修改指定索引的x轴参数) 或 修改指定索引的x轴参数 ≤ json.数组_取长度 (“yAxis”) － 1)  ' 指定的索引留空或者不存在时将创建新x轴，否则将修改指定索引的x轴参数
    索引 ＝ json.数组_加入成员 (“yAxis”, “{}”) － 1
.否则
    索引 ＝ 修改指定索引的x轴参数
.如果结束

' 开启还是关闭
.如果 (是否为空 (开启还是关闭))
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].show”, , , , 真)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].show”, , , , 开启还是关闭)
.如果结束
' 位置
.如果 (是否为空 (位置))
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].position”, , “left”)
.否则
    .如果 (位置)
        json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].position”, , “right”)
    .否则
        json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].position”, , “left”)
    .如果结束

.如果结束
' 是否脱离锁定0刻度
.如果 (是否为空 (是否脱离锁定0刻度))
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].scale”, , , , 假)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].scale”, , , , 是否脱离锁定0刻度)
.如果结束
' 刻度最大值
.如果 (是否为空 (刻度最大值))
    json.删除 (“yAxis[” ＋ 到文本 (索引) ＋ “].max”)
.否则
    .如果 (刻度最大值 ＝ “dataMax”)
        json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].max”, , “dataMax”)
    .否则
        json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].max”, , , 到数值 (刻度最大值))
    .如果结束

.如果结束
' 刻度最小值
.如果 (是否为空 (刻度最小值))
    json.删除 (“yAxis[” ＋ 到文本 (索引) ＋ “].min”)
.否则
    .如果 (刻度最小值 ＝ “dataMin”)
        json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].min”, , “dataMin”)
    .否则
        json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].min”, , , 到数值 (刻度最小值))
    .如果结束

.如果结束
' 刻度分段数
.如果 (是否为空 (刻度分段数))
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].splitNumber”, , , 5)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].splitNumber”, , , 刻度分段数)
.如果结束
' 坐标轴类型
.如果 (是否为空 (坐标轴类型))
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].type”, , “value”)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].type”, , 坐标轴类型)
.如果结束
' 名称
.如果 (是否为空 (名称))
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].name”, , “”)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].name”, , 名称)
.如果结束
' 格式
.如果 (是否为空 (格式))
    json.删除 (“yAxis[” ＋ 到文本 (索引) ＋ “].axisLabel.formatter”)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].axisLabel.formatter”, , 格式)
.如果结束
' 反向坐标轴
.如果 (是否为空 (反向坐标轴))
    json.删除 (“yAxis[” ＋ 到文本 (索引) ＋ “].inverse”)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].inverse”, , , , 反向坐标轴)
.如果结束
' 轴颜色
.如果 (是否为空 (轴颜色))
    json.删除 (“yAxis[” ＋ 到文本 (索引) ＋ “].axisLine.lineStyle.color”)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].axisLine.lineStyle.color”, , 轴颜色)
.如果结束
' 是否锁定0刻度
.如果 (是否为空 (是否锁定0刻度))
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].scale”, , , , 假)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].scale”, , , , 是否锁定0刻度)
.如果结束
' 绑定GRID组件
.如果 (是否为空 (绑定grid组件))
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].gridIndex”, , , 0)
.否则
    json.置新值 (“yAxis[” ＋ 到文本 (索引) ＋ “].gridIndex”, , , 绑定grid组件)
.如果结束

返回 (索引)

.子程序 设置缩放组件, , 公开, 设置X轴或Y轴缩放组件。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 开启X轴缩放条, 逻辑型, , 真为开启，假为不开启。
.参数 开启Y轴缩放条, 逻辑型, 可空, 真为开启，假为不开启。默认不开启
.参数 开启X轴拖动及鼠标缩放, 逻辑型, 可空, 真为开启，假为不开启。默认随X轴缩放条开关
.参数 开启Y轴拖动及鼠标缩放, 逻辑型, 可空, 真为开启，假为不开启。默认随Y轴缩放条开关
.参数 X轴开始位置, 整数型, 可空, 默认为0（范围：0-100)
.参数 X轴结束位置, 整数型, 可空, 默认为100（范围：0-100）
.参数 Y轴开始位置, 整数型, 可空, 默认为0（范围：0-100）
.参数 Y轴结束位置, 整数型, 可空, 默认为100（范围：0-100）
.局部变量 临时数组, 整数型, , "0"
.局部变量 计数器, 整数型
.局部变量 x_start, 整数型
.局部变量 x_end, 整数型
.局部变量 y_start, 整数型
.局部变量 y_end, 整数型

json.置新值 (“dataZoom”, “[]”)
.如果 (是否为空 (X轴开始位置))
    x_start ＝ 0
.否则
    x_start ＝ X轴开始位置
.如果结束
.如果 (是否为空 (X轴结束位置))
    x_end ＝ 100
.否则
    x_end ＝ X轴结束位置
.如果结束
.如果 (是否为空 (Y轴开始位置))
    y_start ＝ 0
.否则
    y_start ＝ Y轴开始位置
.如果结束
.如果 (是否为空 (Y轴结束位置))
    y_end ＝ 100
.否则
    y_end ＝ Y轴结束位置
.如果结束

json.数组_加入成员 (“dataZoom”, “{}”)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].show”, , , , 开启X轴缩放条)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].type”, , “slider”)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].start”, , , x_start)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].end”, , , x_end)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].xAxisIndex”, , , 0)
json.数组_加入成员 (“dataZoom”, “{}”)
.如果 (是否为空 (开启X轴拖动及鼠标缩放))
    json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].disabled”, , , , 取反 (开启X轴缩放条))
.否则
    json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].disabled”, , , , 取反 (开启X轴拖动及鼠标缩放))
.如果结束
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].type”, , “inside”)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].start”, , , x_start)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].end”, , , x_end)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].xAxisIndex”, , , 0)

json.数组_加入成员 (“dataZoom”, “{}”)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].show”, , , , 开启Y轴缩放条)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].type”, , “slider”)
.如果 (json.数组_取长度 (“yAxis”) ＞ 1)
    .计次循环首 (json.数组_取长度 (“yAxis”), 计数器)
        加入成员 (临时数组, 计数器 － 1)
    .计次循环尾 ()
    json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].yAxisIndex”, 创建JSON数组 (, , 临时数组))
.否则
    json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].yAxisIndex”, , , 0)
.如果结束
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].start”, , , y_start)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].end”, , , y_end)

json.数组_加入成员 (“dataZoom”, “{}”)
.如果 (是否为空 (开启Y轴拖动及鼠标缩放))
    json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].disabled”, , , , 取反 (开启Y轴缩放条))
.否则
    json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].disabled”, , , , 取反 (开启Y轴拖动及鼠标缩放))
.如果结束
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].type”, , “inside”)
.如果 (json.数组_取长度 (“yAxis”) ＞ 1)
    .计次循环首 (json.数组_取长度 (“yAxis”), 计数器)
        加入成员 (临时数组, 计数器 － 1)
    .计次循环尾 ()
    json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].yAxisIndex”, 创建JSON数组 (, , 临时数组))
.否则
    json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].yAxisIndex”, , , 0)
.如果结束
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].start”, , , y_start)
json.置新值 (“dataZoom[” ＋ 到文本 (json.数组_取长度 (“dataZoom”) － 1) ＋ “].end”, , , y_end)



.子程序 设置时间轴, eCharts_JSON类, 公开, 设置时间轴，时间轴组件允许自动/手动从多组图形配置option中切换操作。【请注意：本命令请在基本图形参数json设置完毕最后调用。】
.参数 公用配置option, eCharts_JSON类, , 公用图形配置json。
.参数 集合配置option, eCharts_JSON类, 可空, 请使用命令“时间轴集合配置option_添加一个（）”生成，集合option中存放需要切换的多组数据配置。注意其中某个option配置了某个属性时其他option也须同样配置此属性，否则此属性的执行效果会遗留。
.参数 数据组, 文本型, , 时间轴组件数据组，请使用‘创建JSON数组（）’命令生成。
.参数 显示或隐藏, 逻辑型, 可空, 默认为显示。真=显示，假=隐藏（功能依旧存在）。
.参数 轴类型, 文本型, 可空, 默认为“time”时间轴，可选常量：“#时间轴组件类型_**轴”。
.参数 当前选中, 整数型, 可空, 默认为0（从0开始）。
.参数 自动播放, 逻辑型, 可空, 默认为不自动播放。真=自动播放（将自动正向循环播放），假=不自动播放。
.参数 自动播放间隔时间, 整数型, 可空, 默认为2000ms
.参数 左, 文本型, 可空, 默认为“auto”，组件距离容器左侧的距离。可选“left”、“center”、“right”或百分比“20%”或像素值“20px”。
.参数 右, 文本型, 可空, 默认为“auto”，组件距离容器上侧的距离。可选“top”、“middle”、“bottom”或百分比“20%”或像素值“20px”。
.参数 上, 文本型, 可空, 默认为“auto”，组件距离容器右侧的距离。可选百分比“20%”或像素值“20px”。
.参数 下, 文本型, 可空, 默认为“auto”，组件距离容器下侧的距离。可选百分比“20%”或像素值“20px”。
.参数 水平或竖直摆放, 文本型, 可空, 默认为“horizontal”（水平放置），可选常量：“#时间轴组件摆放_**”
.参数 刻度标签旋转角度, 整数型, 可空, 默认不旋转0，角度值：-90~90。用于类目标签不够位置显示时候通过旋转防止标签直接重叠。
.参数 是否显示播放控制按钮, 逻辑型, 可空, 默认不显示，真=显示，假=不显示。
.局部变量 json, eCharts_JSON类

json.解析 ()
json.置新值 (“baseOption”, 公用配置option.取代码 (假))
json.置新值 (“options”, 集合配置option.取代码 (假))
json.置新值 (“baseOption.timeline.data”, 数据组)
.如果 (是否为空 (显示或隐藏))
    json.置新值 (“baseOption.timeline.show”, , , , 真)
.否则
    json.置新值 (“baseOption.timeline.show”, , , , 显示或隐藏)
.如果结束
.如果 (是否为空 (轴类型))
    json.置新值 (“baseOption.timeline.axisType”, , #时间轴组件类型_时间轴)
.否则
    json.置新值 (“baseOption.timeline.axisType”, , 轴类型)
.如果结束
.如果 (是否为空 (当前选中))
    json.置新值 (“baseOption.timeline.currentIndex”, , , 0)
.否则
    json.置新值 (“baseOption.timeline.currentIndex”, , , 当前选中)
.如果结束
.如果 (是否为空 (自动播放))
    json.置新值 (“baseOption.timeline.autoPlay”, , , , 假)
.否则
    json.置新值 (“baseOption.timeline.autoPlay”, , , , 自动播放)
.如果结束
.如果 (是否为空 (自动播放间隔时间))
    json.置新值 (“baseOption.timeline.playInterval”, , , 2000)
.否则
    json.置新值 (“baseOption.timeline.playInterval”, , , 自动播放间隔时间)
.如果结束
.如果 (是否为空 (左))
    json.置新值 (“baseOption.timeline.left”, , “auto”)
.否则
    json.置新值 (“baseOption.timeline.left”, , 左)
.如果结束
.如果 (是否为空 (右))
    json.置新值 (“baseOption.timeline.right”, , “auto”)
.否则
    json.置新值 (“baseOption.timeline.right”, , 右)
.如果结束
.如果 (是否为空 (上))
    json.置新值 (“baseOption.timeline.top”, , “auto”)
.否则
    json.置新值 (“baseOption.timeline.top”, , 上)
.如果结束
.如果 (是否为空 (下))
    json.置新值 (“baseOption.timeline.bottom”, , “auto”)
.否则
    json.置新值 (“baseOption.timeline.bottom”, , 下)
.如果结束
.如果 (是否为空 (水平或竖直摆放))
    json.置新值 (“baseOption.timeline.orient”, , #时间轴组件摆放_水平放置)
.否则
    json.置新值 (“baseOption.timeline.orient”, , 水平或竖直摆放)
.如果结束
.如果 (是否为空 (刻度标签旋转角度))
    json.置新值 (“baseOption.timeline.label.rotate”, , , 0)
.否则
    json.置新值 (“baseOption.timeline.label.rotate”, , , 刻度标签旋转角度)
.如果结束
.如果 (是否为空 (是否显示播放控制按钮))
    json.置新值 (“baseOption.timeline.controlStyle.show”, , , , 假)
.否则
    json.置新值 (“baseOption.timeline.controlStyle.show”, , , , 是否显示播放控制按钮)
.如果结束

返回 (json)

.子程序 设置坐标轴网格grid组件, 整数型, 公开, 返回当前坐标轴grid组件索引值（设置多个坐标轴网格请重复调用本命令）,单坐标轴时本命令可以省略，将自动使用默认参数创建坐标轴。同一个echarts图形允许同时存在多个坐标轴，网格grid组件用于分别控制各个坐标轴系的位置尺寸等。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 指定grid组件索引, 整数型, 可空, 组件索引（从0开始），不指定或索引不存在将添加一个新的grid，指定索引将修改指定grid的配置。
.参数 是否显示, 逻辑型, 可空, 默认：假=不显示。是否显示直角坐标系网格，设置不显示并不影响尺寸位置控制参数，但如需设置直角坐标轴背景或边框颜色，此项需设置为真。
.参数 Z层次, 整数型, 可空, 默认：2。多个坐标轴重叠时此参数将决定哪个grid显示于上层。
.参数 左, 文本型, 可空, 默认：“10%”。grid组件距离容器左侧的距离。本参数可支持像素值、百分比或“top”、“middle”或“bottom”。
.参数 右, 文本型, 可空, 默认：“10%”。grid组件距离容器右侧的距离。本参数可支持像素值、百分比。
.参数 上, 文本型, 可空, 默认：“60px”。grid组件距离容器上侧的距离。本参数可支持像素值、百分比或“top”、“middle”或“bottom”。
.参数 下, 文本型, 可空, 默认：“60px”。grid组件距离容器底部的距离。本参数可支持像素值、百分比。
.参数 宽度, 文本型, 可空, 默认：“auto”。grid组件的宽度，默认自适应。本参数可支持像素值、百分比。
.参数 高度, 文本型, 可空, 默认：“auto”。grid组件的高度，默认自适应。本参数可支持像素值、百分比。
.参数 是否包含刻度标签, 逻辑型, 可空, 默认：假。grid 区域是否包含坐标轴的刻度标签。为真时尺寸参数设置的区域将包含坐标轴标签等所有内容（可防止标签溢出），为假时尺寸参数设置的区域仅包含坐标轴形成的矩形区域（可有效控制坐标轴对齐）
.参数 背景颜色, 文本型, 可空, 默认：“transparent”（透明）。本参数设置生效需设置grid组件显示=真。
.参数 边框颜色, 文本型, 可空, 默认：“#ccc”。本参数设置生效需设置grid组件显示=真。
.局部变量 索引, 整数型

.如果 (是否为空 (指定grid组件索引) ＝ 假 且 json.数组_取长度 (“grid”) ≥ 索引 ＋ 1)
    索引 ＝ 指定grid组件索引
.否则
    索引 ＝ json.数组_加入成员 (“grid”, “{}”) － 1
.如果结束

.如果 (是否为空 (是否显示))
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].show”, , , , 假)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].show”, , , , 是否显示)
.如果结束
.如果 (是否为空 (Z层次))
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].z”, , , 2)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].z”, , , Z层次)
.如果结束
.如果 (是否为空 (左))
    json.删除 (“grid[” ＋ 到文本 (索引) ＋ “].left”)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].left”, , 左)
.如果结束
.如果 (是否为空 (右))
    json.删除 (“grid[” ＋ 到文本 (索引) ＋ “].right”)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].right”, , 右)
.如果结束
.如果 (是否为空 (上))
    json.删除 (“grid[” ＋ 到文本 (索引) ＋ “].top”)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].top”, , 上)
.如果结束
.如果 (是否为空 (下))
    json.删除 (“grid[” ＋ 到文本 (索引) ＋ “].bottom”)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].bottom”, , 下)
.如果结束
.如果 (是否为空 (宽度))
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].width”, , “auto”)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].width”, , 宽度)
.如果结束
.如果 (是否为空 (高度))
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].height”, , “auto”)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].height”, , 高度)
.如果结束
.如果 (是否为空 (是否包含刻度标签))
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].containLabel”, , , , 假)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].containLabel”, , , , 是否包含刻度标签)
.如果结束
.如果 (是否为空 (背景颜色))
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].backgroundColor”, , “transparent”)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].backgroundColor”, , 背景颜色)
.如果结束
.如果 (是否为空 (边框颜色))
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].borderColor”, , “#ccc”)
.否则
    json.置新值 (“grid[” ＋ 到文本 (索引) ＋ “].borderColor”, , 边框颜色)
.如果结束

返回 (索引)

.子程序 时间轴集合配置option_添加一个, , 公开
.参数 集合配置option, eCharts_JSON类, 参考
.参数 要添加的配置option, eCharts_JSON类

.如果真 (集合配置option.数组_取长度 () ＜ 1)
    集合配置option.解析 (“[]”)
.如果真结束
集合配置option.数组_加入成员 (, 要添加的配置option.取代码 (假))

.子程序 开启或关闭工具组件, , 公开, 开启或关闭工具栏组件。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 开启还是关闭工具组件, 逻辑型, , 开启或关闭工具栏组件。
.参数 开启还是关闭_保存图片工具, 逻辑型, 可空, 开启或关闭保存图片工具，显示需开启工具栏组件。
.参数 开启还是关闭_配置项还原工具, 逻辑型, 可空, 开启或关闭配置项还原工具，显示需开启工具栏组件。
.参数 开启还是关闭_数据视图工具, 逻辑型, 可空, 开启或关闭数据视图工具，显示需开启工具栏组件。
.参数 开启还是关闭_区域缩放工具, 逻辑型, 可空, 开启或关闭区域缩放工具，显示需开启工具栏组件。
.参数 开启还是关闭_动态类型切换工具, 逻辑型, 可空, 开启或关闭动态类型切换工具，显示需开启工具栏组件。
.参数 开启还是关闭_选框组件工具, 逻辑型, 可空, 开启或关闭选框组件工具，显示需开启工具栏组件。
.局部变量 计数器, 整数型

.如果 (json.数组_取长度 (“toolbox”) ≥ 1)
    .计次循环首 (json.数组_取长度 (“toolbox”), 计数器)
        json.置新值 (“toolbox[” ＋ 到文本 (计数器 － 1) ＋ “].show”, , , , 开启还是关闭工具组件)
        .如果真 (是否为空 (开启还是关闭_保存图片工具) ＝ 假)
            json.置新值 (“toolbox[” ＋ 到文本 (计数器 － 1) ＋ “].feature.saveAsImage.show”, , , , 开启还是关闭_保存图片工具)
        .如果真结束
        .如果真 (是否为空 (开启还是关闭_配置项还原工具) ＝ 假)
            json.置新值 (“toolbox[” ＋ 到文本 (计数器 － 1) ＋ “].feature.restore.show”, , , , 开启还是关闭_配置项还原工具)
        .如果真结束
        .如果真 (是否为空 (开启还是关闭_数据视图工具) ＝ 假)
            json.置新值 (“toolbox[” ＋ 到文本 (计数器 － 1) ＋ “].feature.dataView.show”, , , , 开启还是关闭_数据视图工具)
        .如果真结束
        .如果真 (是否为空 (开启还是关闭_区域缩放工具) ＝ 假)
            json.置新值 (“toolbox[” ＋ 到文本 (计数器 － 1) ＋ “].feature.dataZoom.show”, , , , 开启还是关闭_区域缩放工具)
        .如果真结束
        .如果真 (是否为空 (开启还是关闭_动态类型切换工具) ＝ 假)
            json.置新值 (“toolbox[” ＋ 到文本 (计数器 － 1) ＋ “].feature.magicType.show”, , , , 开启还是关闭_动态类型切换工具)
        .如果真结束
        .如果真 (是否为空 (开启还是关闭_选框组件工具) ＝ 假)
            json.置新值 (“toolbox[” ＋ 到文本 (计数器 － 1) ＋ “].feature.brush.show”, , , , 开启还是关闭_选框组件工具)
        .如果真结束

    .计次循环尾 ()
.否则
    json.置新值 (“toolbox.show”, , , , 开启还是关闭工具组件)
    .如果真 (是否为空 (开启还是关闭_保存图片工具) ＝ 假)
        json.置新值 (“toolbox.feature.saveAsImage.show”, , , , 开启还是关闭_保存图片工具)
    .如果真结束
    .如果真 (是否为空 (开启还是关闭_配置项还原工具) ＝ 假)
        json.置新值 (“toolbox.feature.restore.show”, , , , 开启还是关闭_配置项还原工具)
    .如果真结束
    .如果真 (是否为空 (开启还是关闭_数据视图工具) ＝ 假)
        json.置新值 (“toolbox.feature.dataView.show”, , , , 开启还是关闭_数据视图工具)
    .如果真结束
    .如果真 (是否为空 (开启还是关闭_区域缩放工具) ＝ 假)
        json.置新值 (“toolbox.feature.dataZoom.show”, , , , 开启还是关闭_区域缩放工具)
    .如果真结束
    .如果真 (是否为空 (开启还是关闭_动态类型切换工具) ＝ 假)
        json.置新值 (“toolbox.feature.magicType.show”, , , , 开启还是关闭_动态类型切换工具)
    .如果真结束
    .如果真 (是否为空 (开启还是关闭_选框组件工具) ＝ 假)
        json.置新值 (“toolbox.feature.brush.show”, , , , 开启还是关闭_选框组件工具)
    .如果真结束

.如果结束


.子程序 设置或读取全局颜色, , 公开, echart将内置11种颜色，图形颜色将依次循环调用，写入颜色组将覆盖原有颜色组。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 修改颜色_位置, 整数型, 可空, 修养颜色数组指定位置的颜色，位置从0开始。
.参数 修改颜色_颜色值, 文本型, 可空, 修养颜色数组指定位置的颜色，颜色格式例如："#FFF"或"#FFFFFF"。
.参数 写入颜色组, 文本型, 参考 可空 数组, 写入颜色数组，图形颜色将依次循环调用，将覆盖原有颜色组，设置本参数将忽略前边修改指定位置颜色参数。
.参数 还原默认颜色组, 逻辑型, 可空, 为真时还原默认颜色组，将忽略上边所有设置颜色的参数。
.参数 读取颜色组, 文本型, 参考 可空 数组, 读取颜色数组。
.局部变量 计数器, 整数型

.如果真 (json.数组_取长度 (“color”) ＝ 0 或 还原默认颜色组)
    json.置新值 (“color”, “['#c23531','#2f4554', '#61a0a8', '#d48265', '#91c7ae','#749f83',  '#ca8622', '#bda29a','#6e7074', '#546570', '#c4ccd3']”)
.如果真结束
.如果 (是否为空 (写入颜色组))
    .如果真 (是否为空 (修改颜色_位置) ＝ 假 且 是否为空 (修改颜色_颜色值) ＝ 假)
        json.置新值 (“color[” ＋ 到文本 (修改颜色_位置) ＋ “]”, , 修改颜色_颜色值)
    .如果真结束

.否则
    json.置新值 (“color”, 创建JSON数组 (写入颜色组))
.如果结束
清除数组 (读取颜色组)
.计次循环首 (json.数组_取长度 (“color”), 计数器)
    加入成员 (读取颜色组, json.数组_取出成员 (“color[” ＋ 到文本 (计数器 － 1) ＋ “]”))
.计次循环尾 ()


.子程序 设置背景颜色, , 公开, 设置背景颜色（渐变或纹理背景请参考官方api后手动设置）。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 颜色值, 文本型, , 颜色格式例如："#FFF"或"#FFFFFF"

json.置新值 (“backgroundColor”, , 颜色值)


.子程序 添加图形数据_折线, 整数型, 公开, 添加一条折线。多条折线时请重复调用添加。返回当前折线索引。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 名称, 文本型
.参数 数据组, 文本型, , 数据组。格式为：“[10,15,64,24...]”，强烈建议由“创建JSON数组()”生成。
.参数 是否显示为散点, 逻辑型, 可空, 默认为假
.参数 是否平滑折线, 逻辑型, 可空, 默认为真
.参数 是否阶梯线图, 逻辑型, 可空, 默认为假
.参数 是否区域填充, 逻辑型, 可空, 默认为假
.参数 是否显示文字标签, 逻辑型, 可空, 默认为假
.参数 添加标注点_最小值, 逻辑型, 可空
.参数 添加标注点_最大值, 逻辑型, 可空
.参数 添加标注点_平均值, 逻辑型, 可空
.参数 添加标线_最小值, 逻辑型, 可空
.参数 添加标线_最大值, 逻辑型, 可空
.参数 添加标线_平均值, 逻辑型, 可空
.参数 绑定x轴索引, 整数型, 可空, 留空将自动绑定索引为0的x轴。
.参数 绑定y轴索引, 整数型, 可空, 留空将自动绑定索引为0的y轴。
.局部变量 索引, 整数型
.局部变量 计数器, 整数型

索引 ＝ json.数组_取长度 (“series”)
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].name”, , 名称)
.如果 (是否为空 (是否显示为散点) ＝ 假 且 是否显示为散点)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].type”, , “scatter”)
.否则
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].type”, , “line”)
.如果结束
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data”, 数据组)
.如果 (是否为空 (是否平滑折线))
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].smooth”, , , , 真)
.否则
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].smooth”, , , , 是否平滑折线)
.如果结束
.如果真 (是否为空 (是否阶梯线图) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].step”, , , , 是否阶梯线图)
.如果真结束
.如果真 (是否为空 (是否区域填充) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].areaStyle.normal”, “”)
.如果真结束
.如果真 (添加标注点_最小值 或 添加标注点_最大值 或 添加标注点_平均值)
    .如果真 (是否为空 (添加标注点_最大值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data[” ＋ 到文本 (计数器) ＋ “].type”, , “max”)
    .如果真结束
    .如果真 (是否为空 (添加标注点_最小值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data[” ＋ 到文本 (计数器) ＋ “].type”, , “min”)
    .如果真结束
    .如果真 (是否为空 (添加标注点_平均值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data[” ＋ 到文本 (计数器) ＋ “].type”, , “average”)
    .如果真结束

.如果真结束
.如果真 (添加标线_最小值 或 添加标线_最大值 或 添加标线_平均值)
    .如果真 (是否为空 (添加标线_最大值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data[” ＋ 到文本 (计数器) ＋ “].type”, , “max”)
    .如果真结束
    .如果真 (是否为空 (添加标线_最小值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data[” ＋ 到文本 (计数器) ＋ “].type”, , “min”)
    .如果真结束
    .如果真 (是否为空 (添加标线_平均值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data[” ＋ 到文本 (计数器) ＋ “].type”, , “average”)
    .如果真结束

.如果真结束
.如果真 (是否为空 (是否显示文字标签) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].label.normal.show”, , , , 是否显示文字标签)
.如果真结束
.如果 (是否为空 (绑定x轴索引))
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].xAxisIndex”, , , 0)
.否则
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].xAxisIndex”, , , 绑定x轴索引)
.如果结束
.如果 (是否为空 (绑定y轴索引))
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].yAxisIndex”, , , 0)
.否则
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].yAxisIndex”, , , 绑定y轴索引)
.如果结束
返回 (索引)

.子程序 添加图形数据_柱图, 整数型, 公开, 添加一组柱图。多组柱图时请重复调用添加。返回当前柱图索引。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 名称, 文本型
.参数 数据组, 文本型, , 数据组。格式为：“[10,15,64,24...]”，强烈建议由“创建JSON数组()”生成。
.参数 是否显示文字标签, 逻辑型, 可空
.参数 添加标注点_最小值, 逻辑型, 可空
.参数 添加标注点_最大值, 逻辑型, 可空
.参数 添加标注点_平均值, 逻辑型, 可空
.参数 添加标线_最小值, 逻辑型, 可空
.参数 添加标线_最大值, 逻辑型, 可空
.参数 添加标线_平均值, 逻辑型, 可空
.参数 绑定x轴索引, 整数型, 可空, 留空将自动绑定索引为0的x轴。
.参数 绑定y轴索引, 整数型, 可空, 留空将自动绑定索引为0的y轴。
.局部变量 索引, 整数型
.局部变量 计数器, 整数型

索引 ＝ json.数组_取长度 (“series”)
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].name”, , 名称)
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].type”, , “bar”)
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data”, 数据组)
.如果真 (添加标注点_最小值 或 添加标注点_最大值 或 添加标注点_平均值)
    .如果真 (是否为空 (添加标注点_最大值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data[” ＋ 到文本 (计数器) ＋ “].type”, , “max”)
    .如果真结束
    .如果真 (是否为空 (添加标注点_最小值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data[” ＋ 到文本 (计数器) ＋ “].type”, , “min”)
    .如果真结束
    .如果真 (是否为空 (添加标注点_平均值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markPoint.data[” ＋ 到文本 (计数器) ＋ “].type”, , “average”)
    .如果真结束

.如果真结束
.如果真 (添加标线_最小值 或 添加标线_最大值 或 添加标线_平均值)
    .如果真 (是否为空 (添加标线_最大值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data[” ＋ 到文本 (计数器) ＋ “].type”, , “max”)
    .如果真结束
    .如果真 (是否为空 (添加标线_最小值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data[” ＋ 到文本 (计数器) ＋ “].type”, , “min”)
    .如果真结束
    .如果真 (是否为空 (添加标线_平均值) ＝ 假)
        json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”, “{}”)
        计数器 ＝ json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data”) － 1
        json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].markLine.data[” ＋ 到文本 (计数器) ＋ “].type”, , “average”)
    .如果真结束

.如果真结束
.如果真 (是否为空 (是否显示文字标签) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].label.normal.show”, , , , 是否显示文字标签)
.如果真结束
.如果 (是否为空 (绑定x轴索引))
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].xAxisIndex”, , , 0)
.否则
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].xAxisIndex”, , , 绑定x轴索引)
.如果结束
.如果 (是否为空 (绑定y轴索引))
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].yAxisIndex”, , , 0)
.否则
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].yAxisIndex”, , , 绑定y轴索引)
.如果结束
返回 (索引)

.子程序 添加图形数据_饼图, 整数型, 公开, 添加一组饼图。多组饼图时请重复调用添加。返回当前饼图索引。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 名称组, 文本型, 数组, 名称组。注意：名称组内容顺序与个数请与数据组一一对应。
.参数 数据组, 双精度小数型, 数组, 数据组。注意：数据组内容顺序与个数请与名称组一一对应。
.参数 是否显示文字标签, 逻辑型, 可空
.参数 是否显示引导标线, 逻辑型, 可空
.参数 是否南丁格尔图, 文本型, 可空, 默认为不显示：false，可选“radius”/“area”。“radius”：圆心角展示数据百分比，半径展现数据大小；“area”：圆心角相同，仅通过半径展现数据大小。
.参数 圆心坐标_横, 文本型, 可空, 默认为：“50%”。可选像素值或百分比，例如："350"或"25%"。注意：请与纵坐标同时设置，否则将忽略。
.参数 圆心坐标_纵, 文本型, 可空, 默认为：“50%”。可选像素值或百分比，例如："350"或"25%"。注意：请与横坐标同时设置，否则将忽略。
.参数 饼图半径_内半径, 文本型, 可空, 默认为：“0”。可选像素值或百分比，例如："50"或"50%"。注意：请与外半径同时设置，否则将忽略。
.参数 饼图半径_外半径, 文本型, 可空, 默认为：“75%”。可选像素值或百分比，例如：“80”或“90%”。注意：请与内半径同时设置，否则将忽略。
.局部变量 索引, 整数型
.局部变量 参数, 文本型
.局部变量 计数器, 整数型
.局部变量 seriesIndex, 整数型

json.数组_加入成员 (“series”, “{}”)
索引 ＝ json.数组_取长度 (“series”) － 1
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].type”, , “pie”)
.计次循环首 (取数组成员数 (名称组), 计数器)
    json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].data”, “{}”)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].name”, , 名称组 [计数器])
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].value”, , , 数据组 [计数器])
.计次循环尾 ()
.如果真 (是否为空 (是否显示文字标签) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].label.normal.show”, , , , 是否显示文字标签)
.如果真结束
.如果真 (是否为空 (是否显示引导标线) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].labelLine.normal.show”, , , , 是否显示引导标线)
.如果真结束
.如果真 (是否为空 (是否南丁格尔图) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].roseType”, , 是否南丁格尔图)
.如果真结束
.如果真 (是否为空 (圆心坐标_横) ＝ 假 且 是否为空 (圆心坐标_纵) ＝ 假)
    参数 ＝ “[参数1,参数2]”
    .如果 (寻找文本 (圆心坐标_横, “%”, , 假) ≠ -1)
        参数 ＝ 子文本替换 (参数, “参数1”, “'” ＋ 圆心坐标_横 ＋ “'”, , , 真)
    .否则
        参数 ＝ 子文本替换 (参数, “参数1”, 圆心坐标_横, , , 真)
    .如果结束
    .如果 (寻找文本 (圆心坐标_纵, “%”, , 假) ≠ -1)
        参数 ＝ 子文本替换 (参数, “参数2”, “'” ＋ 圆心坐标_纵 ＋ “'”, , , 真)
    .否则
        参数 ＝ 子文本替换 (参数, “参数2”, 圆心坐标_纵, , , 真)
    .如果结束
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].center”, 参数)
.如果真结束
.如果真 (是否为空 (饼图半径_内半径) ＝ 假 且 是否为空 (饼图半径_外半径) ＝ 假)
    参数 ＝ “[参数1,参数2]”
    .如果 (寻找文本 (饼图半径_内半径, “%”, , 假) ≠ -1)
        参数 ＝ 子文本替换 (参数, “参数1”, “'” ＋ 饼图半径_内半径 ＋ “'”, , , 真)
    .否则
        参数 ＝ 子文本替换 (参数, “参数1”, 饼图半径_内半径, , , 真)
    .如果结束
    .如果 (寻找文本 (饼图半径_外半径, “%”, , 假) ≠ -1)
        参数 ＝ 子文本替换 (参数, “参数2”, “'” ＋ 饼图半径_外半径 ＋ “'”, , , 真)
    .否则
        参数 ＝ 子文本替换 (参数, “参数2”, 饼图半径_外半径, , , 真)
    .如果结束
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].radius”, 参数)
.如果真结束
返回 (索引)

.子程序 添加图形数据_雷达图, , 公开, 添加雷达图数据。默认支持添加三组数据，请不要重复调用本参数，如需添加更多数据请使用json类手动创建。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 坐标系_名称组, 文本型, 数组, 坐标系各维度名称组。数据组实际显示维度将由坐标系名称组成员数限制。注意：本参数成员数需与最大值组成员数相同。
.参数 坐标系_最大值组, 双精度小数型, 数组, 坐标系的最大值组。数据组实际显示维度将由坐标系名称组成员数限制。注意：本参数成员数需与名称组成员数相同。
.参数 数据组1_名称, 文本型, , 名称
.参数 数据组1_数据, 双精度小数型, 数组, 实际数组组。数据组实际显示维度将由坐标系名称组成员数限制。
.参数 数据组2_名称, 文本型, 可空, 可选。
.参数 数据组2_数据, 双精度小数型, 可空 数组, 可选。格式参考数据组1.
.参数 数据组3_名称, 文本型, 可空, 可选。
.参数 数据组3_数据, 双精度小数型, 可空 数组, 可选。格式参考数据组1.
.参数 坐标系类型_多边形或圆形, 逻辑型, 可空, 默认为假=自适应多边形。为真=圆形。
.参数 是否开启区域填充, 逻辑型, 可空, 开启全部区域填充。
.局部变量 索引, 整数型
.局部变量 计数器, 整数型
.局部变量 参数, 文本型

json.数组_加入成员 (“series”, “{}”)
索引 ＝ json.数组_取长度 (“series”) － 1
.如果真 (坐标系类型_多边形或圆形)
    json.置新值 (“radar.shape”, , “circle”)
.如果真结束
.如果 (取数组成员数 (坐标系_名称组) ≥ 取数组成员数 (坐标系_最大值组))
    .计次循环首 (取数组成员数 (坐标系_名称组), 计数器)
        json.数组_加入成员 (“radar.indicator”, “{}”)
        json.置新值 (“radar.indicator[” ＋ 到文本 (json.数组_取长度 (“radar.indicator”) － 1) ＋ “].name”, , 坐标系_名称组 [计数器])
        json.置新值 (“radar.indicator[” ＋ 到文本 (json.数组_取长度 (“radar.indicator”) － 1) ＋ “].max”, , , 坐标系_最大值组 [计数器])
    .计次循环尾 ()
.否则
    .计次循环首 (取数组成员数 (坐标系_最大值组), 计数器)
        json.数组_加入成员 (“radar.indicator”, “{}”)
        json.置新值 (“radar.indicator[” ＋ 到文本 (json.数组_取长度 (“radar.indicator”) － 1) ＋ “].name”, , 坐标系_名称组 [计数器])
        json.置新值 (“radar.indicator[” ＋ 到文本 (json.数组_取长度 (“radar.indicator”) － 1) ＋ “].max”, , , 坐标系_最大值组 [计数器])
    .计次循环尾 ()
.如果结束
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].type”, , “radar”)
.如果真 (是否开启区域填充)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].areaStyle.normal”, “{}”)
.如果真结束
json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].data”, “{}”)
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].name”, , 数据组1_名称)
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].value”, 创建JSON数组 (, 数据组1_数据))
.如果真 (是否为空 (数据组2_名称) ＝ 假 且 是否为空 (数据组2_数据) ＝ 假)
    json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].data”, “{}”)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].name”, , 数据组2_名称)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].value”, 创建JSON数组 (, 数据组2_数据))
.如果真结束
.如果真 (是否为空 (数据组3_名称) ＝ 假 且 是否为空 (数据组3_数据) ＝ 假)
    json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].data”, “{}”)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].name”, , 数据组3_名称)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].value”, 创建JSON数组 (, 数据组3_数据))
.如果真结束


.子程序 开启或关闭鼠标提示组件, , 公开
.参数 json, eCharts_JSON类, 参考
.参数 开启还是关闭, 逻辑型
.参数 触发类型_数据项或坐标轴, 逻辑型, 可空, 默认为假=数据项"tiem"，真=坐标轴"axis"。数据项图形触发，主要在散点图，饼图等无类目轴的图表中使用。坐标轴触发，主要在柱状图，折线图等会使用类目轴的图表中使用。
.参数 坐标指示器类型, 文本型, 可空, 默认为直线指示器"line"，可选常量：#坐标指示器类型_***。

json.置新值 (“tooltip.show”, , , , 开启还是关闭)
.如果 (触发类型_数据项或坐标轴)
    json.置新值 (“tooltip.trigger”, , “axis”)
.否则
    json.置新值 (“tooltip.trigger”, , “item”)
.如果结束
.如果真 (是否为空 (坐标指示器类型) ＝ 假)
    json.置新值 (“tooltip.axisPointer.type”, , 坐标指示器类型)
.如果真结束


.子程序 添加图形数据_仪表盘, 整数型, 公开, 添加一组仪表盘。多个仪表盘时请重复调用添加。返回当前仪表盘索引。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 名称, 文本型, 可空
.参数 数值, 双精度小数型
.参数 单位, 文本型, 可空
.参数 半径百分比, 双精度小数型, 可空, 仪表盘半径，可以是相对于容器高宽中较小的一项的一半的百分比。
.参数 位置横坐标百分比, 双精度小数型, 可空, 默认50，必须与纵坐标同时设置。
.参数 位置纵坐标百分比, 双精度小数型, 可空, 默认50，必须与横坐标同时设置。
.参数 刻度盘起始角度, 双精度小数型, 可空, 默认为225，圆心正右手侧为0度，正上方为90度，正左手侧为180度。
.参数 刻度盘结束角度, 双精度小数型, 可空, 默认为-45，圆心 正右手侧为0度，正上方为90度，正左手侧为180度。
.参数 刻度盘最小值, 双精度小数型, 可空, 默认为0
.参数 刻度盘最大值, 双精度小数型, 可空, 默认为100
.参数 刻度盘分段, 双精度小数型, 可空, 默认为10
.参数 是否显示实际数值, 逻辑型, 可空, 默认真=显示，假=隐藏。
.参数 刻度盘分段颜色, 分段颜色, 可空 数组, 手动指定分段颜色。
.局部变量 索引, 整数型
.局部变量 参数, 文本型
.局部变量 计数器, 整数型
.局部变量 temp, eCharts_JSON类

json.数组_加入成员 (“series”, “{}”)
索引 ＝ json.数组_取长度 (“series”) － 1
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].type”, , “gauge”)
.如果真 (是否为空 (单位) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].detail.formatter”, , “{value}” ＋ 单位)
.如果真结束
json.数组_加入成员 (“series[” ＋ 到文本 (索引) ＋ “].data”, “{}”)
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].value”, , , 数值)
.如果真 (是否为空 (名称) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data[” ＋ 到文本 (json.数组_取长度 (“series[” ＋ 到文本 (索引) ＋ “].data”) － 1) ＋ “].name”, , 名称)
.如果真结束
.如果真 (是否为空 (半径百分比) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].radius”, , 到文本 (半径百分比) ＋ “%”)
.如果真结束
.如果真 (是否为空 (位置横坐标百分比) ＝ 假 且 是否为空 (位置纵坐标百分比) ＝ 假)
    参数 ＝ “['” ＋ 到文本 (位置横坐标百分比) ＋ “%','” ＋ 到文本 (位置纵坐标百分比) ＋ “%']”
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].center”, 参数)
.如果真结束
.如果真 (是否为空 (刻度盘起始角度) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].startAngle”, , , 刻度盘起始角度)
.如果真结束
.如果真 (是否为空 (刻度盘结束角度) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].endAngle”, , , 刻度盘结束角度)
.如果真结束
.如果真 (是否为空 (刻度盘最小值) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].min”, , , 刻度盘最小值)
.如果真结束
.如果真 (是否为空 (刻度盘最大值) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].max”, , , 刻度盘最大值)
.如果真结束
.如果真 (是否为空 (刻度盘分段) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].splitNumber”, , , 刻度盘分段)
.如果真结束
.如果真 (是否为空 (是否显示实际数值) ＝ 假)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].detail.show”, , , , 是否显示实际数值)
.如果真结束
' json.置新值 (“series[0].axisLine.lineStyle.color”, “[[0.2, '#E43F3D'],[0.4, '#E98E2C'],[0.6, '#DDBD4D'],[0.8, '#7CBB55'],[1, '#9CD6CE']]”)
.如果真 (是否为空 (刻度盘分段颜色) ＝ 假)
    temp.解析 (“[]”)
    .计次循环首 (取数组成员数 (刻度盘分段颜色), 计数器)
        temp.数组_加入成员 (“”, “[” ＋ 到文本 (刻度盘分段颜色 [计数器].分段位置 ÷ 100) ＋ “,'” ＋ 刻度盘分段颜色 [计数器].颜色 ＋ “']”)
    .计次循环尾 ()
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].axisLine.lineStyle.color”, temp.取代码 ())
.如果真结束
返回 (索引)

.子程序 添加图形数据_K线, , 公开, 添加股票K线图形。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 数据组, K线数据格式, 可空 数组, K线数据组。
.参数 均线天数MA, 文本型, 可空, 为空则不显示，也支持多个，以符号“+”分割，例如：“5+10+20+30”，计算价格均线将影响K线生成时间（大约增加10%~30%）
.参数 数据组占位变量, 逻辑型, 可空, 将填写为“##占位##”，用于后续替换成js变量名（数据量比较大时可以节省加载时间），填写此参数后将忽略上边“数据组”参数。
.局部变量 临时小数数组, 双精度小数型, , "0"
.局部变量 临时文本数组, 文本型, , "0"
.局部变量 MA数组, 整数型, , "0"
.局部变量 索引, 整数型
.局部变量 计数器, 整数型
.局部变量 temp, eCharts_JSON类

.如果真 (是否为空 (均线天数MA) ＝ 假)
    临时文本数组 ＝ 分割文本 (均线天数MA, “+”, )
    .计次循环首 (取数组成员数 (临时文本数组), 计数器)
        .如果真 (到整数 (临时文本数组 [计数器]) ＞ 0)
            加入成员 (MA数组, 到整数 (临时文本数组 [计数器]))
        .如果真结束

    .计次循环尾 ()
.如果真结束

temp.解析 (“[]”)
.计次循环首 (取数组成员数 (数据组), 计数器)
    清除数组 (临时小数数组)
    加入成员 (临时小数数组, 数据组 [计数器].开盘open)
    加入成员 (临时小数数组, 数据组 [计数器].收盘close)
    加入成员 (临时小数数组, 数据组 [计数器].最低lowest)
    加入成员 (临时小数数组, 数据组 [计数器].最高highest)
    temp.数组_加入成员 (“”, 创建JSON数组 (, 临时小数数组))

.计次循环尾 ()

json.数组_加入成员 (“series”, “{}”)
索引 ＝ json.数组_取长度 (“series”) － 1
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].name”, , “日K”)
json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].type”, , “candlestick”)
.如果 (是否为空 (数据组占位变量))
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data”, temp.取代码 ())
.否则
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data”, , “##占位##”)
.如果结束

.计次循环首 (取数组成员数 (MA数组), 计数器)
    json.数组_加入成员 (“series”, “{}”)
    索引 ＝ json.数组_取长度 (“series”) － 1
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].name”, , “MA” ＋ 到文本 (MA数组 [计数器]))
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].type”, , “line”)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].data”, 计算_K线图价格均线MA (数据组, MA数组 [计数器]))
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].smooth”, , , , 真)
    json.置新值 (“series[” ＋ 到文本 (索引) ＋ “].lineStyle.normal.opacity”, , , 0.5)
.计次循环尾 ()


.子程序 添加图形数据_词云图, , 公开, 添加词云图。
.参数 json, eCharts_JSON类, 参考
.参数 词条名称组, 文本型, 数组, 名称组内容与数据组必须一一对应。
.参数 词条数据组, 整数型, 数组, 名称组内容与数据组必须一一对应。
.参数 词条随机颜色, 逻辑型, 可空, 可空，默认开启。
.参数 词条阴影, 逻辑型, 可空, 可空，默认开启
.参数 词条间隔大小, 整数型, 可空, 可空，默认20。
.参数 词条尺寸范围_最小, 整数型, 可空, 可空，默认12。词条随机尺寸的范围最小值。
.参数 词条尺寸范围_最大, 整数型, 可空, 可空，默认50。词条随机尺寸的范围最大值。
.参数 词条旋转角度范围_最小, 整数型, 可空, 可空，默认0（不旋转）。词条随机旋转角度的范围最小值。
.参数 词条旋转角度范围_最大, 整数型, 可空, 可空，默认0（不旋转）。词条随机旋转角度的范围最大值。
.局部变量 i, 整数型
.局部变量 次数, 整数型
.局部变量 temp_json, eCharts_JSON类

json.解析 ()
json.置新值 (“series[0].type”, , “wordCloud”)
.如果 (是否为空 (词条间隔大小))
    json.置新值 (“series[0].gridSize”, , , 20)
.否则
    json.置新值 (“series[0].gridSize”, , , 词条间隔大小)
.如果结束
.如果 (是否为空 (词条尺寸范围_最小))
    json.置新值 (“series[0].sizeRange[0]”, , , 12)
.否则
    json.置新值 (“series[0].sizeRange[0]”, , , 词条尺寸范围_最小)
.如果结束
.如果 (是否为空 (词条尺寸范围_最大))
    json.置新值 (“series[0].sizeRange[1]”, , , 50)
.否则
    json.置新值 (“series[0].sizeRange[1]”, , , 词条尺寸范围_最大)
.如果结束
.如果 (是否为空 (词条旋转角度范围_最小))
    json.置新值 (“series[0].rotationRange[0]”, , , 0)
.否则
    json.置新值 (“series[0].rotationRange[0]”, , , 词条旋转角度范围_最小)
.如果结束
.如果 (是否为空 (词条旋转角度范围_最大))
    json.置新值 (“series[0].rotationRange[1]”, , , 0)
.否则
    json.置新值 (“series[0].rotationRange[1]”, , , 词条旋转角度范围_最大)
.如果结束
.如果 (取数组成员数 (词条数据组) ＜ 取数组成员数 (词条名称组))
    次数 ＝ 取数组成员数 (词条数据组)
.否则
    次数 ＝ 取数组成员数 (词条名称组)
.如果结束
.如果真 (是否为空 (词条阴影) 或 词条阴影)
    json.置新值 (“series[0].textStyle.emphasis.shadowBlur”, , , 10)
    json.置新值 (“series[0].textStyle.emphasis.shadowColor”, , “#333”)
.如果真结束
置随机数种子 (取小时 (取现行时间 ()) × 取秒 (取现行时间 ()))
.计次循环首 (次数, i)
    temp_json.解析 ()
    temp_json.置新值 (“name”, , 词条名称组 [i])
    temp_json.置新值 (“value”, , , 词条数据组 [i])
    temp_json.置新值 (“textStyle.emphasis.color”, , “red”)
    .如果真 (是否为空 (词条随机颜色) 或 词条随机颜色)
        temp_json.置新值 (“textStyle.normal.color”, , “rgb(” ＋ 到文本 (取随机数 (0, 255)) ＋ “,” ＋ 到文本 (取随机数 (0, 255)) ＋ “,” ＋ 到文本 (取随机数 (0, 255)) ＋ “)”)
    .如果真结束
    json.数组_加入成员 (“series[0].data”, temp_json.取代码 ())
.计次循环尾 ()


.子程序 添加图形数据_关系图, , 公开, 添加关系图。
.参数 json, eCharts_JSON类, 参考
.参数 关系节点组, 关系节点, 数组
.参数 对应关系组, 对应关系, 数组
.局部变量 分类组, 文本型, , "0"
.局部变量 分类编号, 文本型, , "0"
.局部变量 计数器1, 整数型
.局部变量 计数器2, 整数型
.局部变量 是否存在, 逻辑型

json.解析 ()
json.置新值 (“series”, “[]”)
json.置新值 (“series[0].type”, , “graph”)
json.置新值 (“series[0].layout”, , “force”)
json.置新值 (“series[0].symbolSize”, , , 45)
json.置新值 (“series[0].focusNodeAdjacency”, , , , 真)
json.置新值 (“series[0].roam”, , , , 真)
json.置新值 (“series[0].draggable”, , , , 真)
json.置新值 (“series[0].label.normal.show”, , , , 真)
json.置新值 (“series[0].label.normal.textStyle.fontSize”, , , 12)
json.置新值 (“series[0].force.repulsion”, , , 1000)
json.置新值 (“series[0].edgeSymbolSize[0]”, , , 4)
json.置新值 (“series[0].edgeSymbolSize[1]”, , , 50)
json.置新值 (“series[0].edgeLabel.normal.show”, , , , 真)
json.置新值 (“series[0].edgeLabel.normal.textStyle.fontSize”, , , 10)
json.置新值 (“series[0].edgeLabel.normal.formatter”, , “{c}”)
json.置新值 (“series[0].lineStyle.normal.opacity”, , , 0.9)
json.置新值 (“series[0].lineStyle.normal.width”, , , 1)
json.置新值 (“series[0].lineStyle.normal.curveness”, , , 0)
.计次循环首 (取数组成员数 (关系节点组), 计数器1)
    是否存在 ＝ 假
    .计次循环首 (取数组成员数 (分类组), 计数器2)
        .如果真 (分类组 [计数器2] ＝ 关系节点组 [计数器1].节点名称)
            是否存在 ＝ 真
        .如果真结束

    .计次循环尾 ()
    .如果真 (是否存在 ＝ 假)
        加入成员 (分类组, 关系节点组 [计数器1].节点名称)
    .如果真结束

.计次循环尾 ()
.计次循环首 (取数组成员数 (分类组), 计数器1)
    json.置新值 (“series[0].categories[” ＋ 到文本 (计数器1 － 1) ＋ “].name”, , 分类组 [计数器1])
.计次循环尾 ()
.计次循环首 (取数组成员数 (关系节点组), 计数器1)
    json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].name”, , 关系节点组 [计数器1].节点名称)
    .如果真 (关系节点组 [计数器1].节点数值 ≠ “”)
        json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].value”, , , 到数值 (关系节点组 [计数器1].节点名称))
    .如果真结束
    .如果真 (关系节点组 [计数器1].节点样式 ≠ “”)
        .判断开始 (关系节点组 [计数器1].节点样式 ＝ “1”)
            json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbol”, , “circle”)
        .判断 (关系节点组 [计数器1].节点样式 ＝ “2”)
            json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbol”, , “rect”)
        .判断 (关系节点组 [计数器1].节点样式 ＝ “3”)
            json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbol”, , “roundRect”)
        .判断 (关系节点组 [计数器1].节点样式 ＝ “4”)
            json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbol”, , “triangle”)
        .判断 (关系节点组 [计数器1].节点样式 ＝ “5”)
            json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbol”, , “diamond”)
        .判断 (关系节点组 [计数器1].节点样式 ＝ “6”)
            json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbol”, , “pin”)
        .判断 (关系节点组 [计数器1].节点样式 ＝ “7”)
            json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbol”, , “arrow”)
        .默认
            json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbol”, , 关系节点组 [计数器1].节点样式)
        .判断结束

    .如果真结束
    .如果 (关系节点组 [计数器1].节点图标大小 ＝ “”)
        json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbolSize”, , , 50)
    .否则
        json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].symbolSize”, , , 到数值 (关系节点组 [计数器1].节点图标大小))
    .如果结束
    .如果真 (关系节点组 [计数器1].节点是否固定 ＝ “1”)
        json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].fixed”, , , , 真)
    .如果真结束
    .计次循环首 (取数组成员数 (分类组), 计数器2)
        .如果真 (分类组 [计数器2] ＝ 关系节点组 [计数器1].节点名称)
            跳出循环 ()
        .如果真结束

    .计次循环尾 ()
    json.置新值 (“series[0].data[” ＋ 到文本 (计数器1 － 1) ＋ “].category”, , , 计数器2 － 1)
.计次循环尾 ()
.计次循环首 (取数组成员数 (对应关系组), 计数器1)
    json.置新值 (“series[0].links[” ＋ 到文本 (计数器1 － 1) ＋ “].value”, , 对应关系组 [计数器1].关系名称)
    json.置新值 (“series[0].links[” ＋ 到文本 (计数器1 － 1) ＋ “].source”, , , 对应关系组 [计数器1].关系节点成员编号1 － 1)
    json.置新值 (“series[0].links[” ＋ 到文本 (计数器1 － 1) ＋ “].target”, , , 对应关系组 [计数器1].关系节点成员编号2 － 1)
.计次循环尾 ()

.子程序 添加图形数据_树图, , 公开, 添加树图。
.参数 json, eCharts_JSON类, 参考
.参数 树图json, eCharts_JSON类, , 数据json必须照固定格式，格式请参考示例。
.参数 展开方向, 整数型, 可空, 默认为0（水平右侧展开），1=垂直下方展开
.参数 标记图形类型, 文本型, 可空, 默认为"0"=空心圆，"1"=圆形，"2"=矩形，"3"=圆角矩形，"4"=三角形，"5"=菱形，"6"=大头针，"7"=箭头，同时支持"image://url"指定图片或者"path://"指定矢量图形。
.参数 标记图形大小, 整数型, 可空, 默认=7
.参数 初始展开层级, 整数型, 可空, 默认=2，默认只有前2层展开，以后的默认是折叠状态
.参数 顶部留空距离, 整数型, 可空, 百分比，默认为12%
.参数 底部留空距离, 整数型, 可空, 百分比，默认为12%
.参数 左侧留空距离, 整数型, 可空, 百分比，默认为12%
.参数 右侧留空距离, 整数型, 可空, 百分比，默认为12%

json.解析 (“{}”)
json.置新值 (“series[0].type”, , “tree”)
json.置新值 (“series[0].data”, “[” ＋ 树图json.取代码 () ＋ “]”)  ' 数据json必须照固定格式
json.置新值 (“series[0].expandAndCollapse”, , , , 真)
json.置新值 (“series[0].animationDuration”, , , 550)
json.置新值 (“series[0].animationDurationUpdate”, , , 750)
.如果 (是否为空 (初始展开层级))
    json.置新值 (“series[0].initialTreeDepth”, , , 2)
.否则
    json.置新值 (“series[0].initialTreeDepth”, , , 初始展开层级)
.如果结束
.如果 (是否为空 (展开方向) 或 展开方向 ＝ 0)
    json.置新值 (“series[0].orient”, , “horizontal”)
.否则
    json.置新值 (“series[0].orient”, , “vertical”)
.如果结束
.判断开始 (是否为空 (标记图形类型))
    json.置新值 (“series[0].symbol”, , “emptyCircle”)
.判断 (标记图形类型 ＝ “0”)
    json.置新值 (“series[0].symbol”, , “emptyCircle”)
.判断 (标记图形类型 ＝ “1”)
    json.置新值 (“series[0].symbol”, , “circle”)
.判断 (标记图形类型 ＝ “2”)
    json.置新值 (“series[0].symbol”, , “rect”)
.判断 (标记图形类型 ＝ “3”)
    json.置新值 (“series[0].symbol”, , “roundRect”)
.判断 (标记图形类型 ＝ “4”)
    json.置新值 (“series[0].symbol”, , “triangle”)
.判断 (标记图形类型 ＝ “5”)
    json.置新值 (“series[0].symbol”, , “diamond”)
.判断 (标记图形类型 ＝ “6”)
    json.置新值 (“series[0].symbol”, , “pin”)
.判断 (标记图形类型 ＝ “7”)
    json.置新值 (“series[0].symbol”, , “arrow”)
.默认
    json.置新值 (“series[0].symbol”, , 标记图形类型)
.判断结束
.如果 (是否为空 (标记图形大小))
    json.置新值 (“series[0].symbolSize”, , , 7)
.否则
    json.置新值 (“series[0].symbolSize”, , , 标记图形大小)
.如果结束
.如果 (是否为空 (顶部留空距离))
    json.置新值 (“series[0].top”, , “12%”)
.否则
    json.置新值 (“series[0].top”, , 到文本 (顶部留空距离) ＋ “%”)
.如果结束
.如果 (是否为空 (底部留空距离))
    json.置新值 (“series[0].bottom”, , “12%”)
.否则
    json.置新值 (“series[0].bottom”, , 到文本 (底部留空距离) ＋ “%”)
.如果结束
.如果 (是否为空 (左侧留空距离))
    json.置新值 (“series[0].left”, , “12%”)
.否则
    json.置新值 (“series[0].left”, , 到文本 (左侧留空距离) ＋ “%”)
.如果结束
.如果 (是否为空 (右侧留空距离))
    json.置新值 (“series[0].right”, , “12%”)
.否则
    json.置新值 (“series[0].right”, , 到文本 (右侧留空距离) ＋ “%”)
.如果结束



.子程序 添加标注线, , 公开, 添加标注线，多条标注线请重复调用添加。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 图形索引, 整数型, , 索引从0开始。
.参数 起点标注类型, 整数型, 可空, 0：自定义（默认，且终点标注自动失效）；1：最小min；2：最大max；3：平均average
.参数 标注位置, 整数型, 可空, 类型为0时有效
.参数 终点标注类型, 整数型, 可空, 1：最小min；2：最大max；3：平均average。注意终点标注仅起点标注非自定义时有效。
.局部变量 索引, 整数型
.局部变量 起点类型, 文本型
.局部变量 终点类型, 文本型

.如果 (是否为空 (终点标注类型))
    索引 ＝ json.数组_加入成员 (“series[” ＋ 到文本 (图形索引) ＋ “].markLine.data”, “{}”) － 1
    .判断开始 (起点标注类型 ＝ 0)
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markLine.data[” ＋ 到文本 (索引) ＋ “].yAxis”, , , 标注位置)
    .判断 (起点标注类型 ＝ 1)
        起点类型 ＝ “min”
    .判断 (起点标注类型 ＝ 2)
        起点类型 ＝ “max”
    .判断 (起点标注类型 ＝ 3)
        起点类型 ＝ “average”
    .默认
        返回 ()
    .判断结束
    .如果真 (起点标注类型 ＝ 1 或 起点标注类型 ＝ 2 或 起点标注类型 ＝ 3)
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markLine.data[” ＋ 到文本 (索引) ＋ “].type”, , 起点类型)
    .如果真结束

.否则
    .如果真 (起点标注类型 ＝ 0)
        返回 ()
    .如果真结束
    索引 ＝ json.数组_加入成员 (“series[” ＋ 到文本 (图形索引) ＋ “].markLine.data”, “[]”) － 1
    .判断开始 (起点标注类型 ＝ 1)
        起点类型 ＝ “min”
    .判断 (起点标注类型 ＝ 2)
        起点类型 ＝ “max”
    .判断 (起点标注类型 ＝ 3)
        起点类型 ＝ “average”
    .默认
        返回 ()
    .判断结束
    .判断开始 (终点标注类型 ＝ 1)
        终点类型 ＝ “min”
    .判断 (终点标注类型 ＝ 2)
        终点类型 ＝ “max”
    .判断 (终点标注类型 ＝ 3)
        终点类型 ＝ “average”
    .默认
        返回 ()
    .判断结束
    json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markLine.data[” ＋ 到文本 (索引) ＋ “][0].type”, , 起点类型)
    json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markLine.data[” ＋ 到文本 (索引) ＋ “][1].type”, , 终点类型)
.如果结束



.子程序 添加标注点, , 公开, 添加标注点，多个标注点请重复调用添加。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 图形索引, 整数型, , 索引从0开始。
.参数 标注类型, 整数型, 可空, 0：自定义（默认，且终点标注自动失效）；1：最小min；2：最大max；3：平均average
.参数 标注点x轴位置, 文本型, 可空, 类型为0时有效，必须与x轴上的数据对应。
.参数 标注点y轴位置, 双精度小数型, 可空, 类型为0时有效，可自由指定。
.参数 标注值, 文本型, 可空, 类型为0时有效，省略将默认显示y轴位置值。
.局部变量 索引, 整数型
.局部变量 类型, 文本型

索引 ＝ json.数组_加入成员 (“series[” ＋ 到文本 (图形索引) ＋ “].markPoint.data”, “{}”) － 1
.判断开始 (标注类型 ＝ 0)
    json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markPoint.data[” ＋ 到文本 (索引) ＋ “].coord[0]”, , 标注点x轴位置)
    json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markPoint.data[” ＋ 到文本 (索引) ＋ “].coord[1]”, , , 标注点y轴位置)
    .如果 (是否为空 (标注值))
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markPoint.data[” ＋ 到文本 (索引) ＋ “].value”, , 到文本 (标注点y轴位置))
    .否则
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markPoint.data[” ＋ 到文本 (索引) ＋ “].value”, , 标注值)
    .如果结束

.判断 (标注类型 ＝ 1)
    类型 ＝ “min”
.判断 (标注类型 ＝ 2)
    类型 ＝ “max”
.判断 (标注类型 ＝ 3)
    类型 ＝ “average”
.默认
    返回 ()
.判断结束
.如果真 (标注类型 ＝ 1 或 标注类型 ＝ 2 或 标注类型 ＝ 3)
    json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markPoint.data[” ＋ 到文本 (索引) ＋ “].type”, , 类型)
.如果真结束


.子程序 添加标注区域, , 公开, 添加标注区域，多个标注区域请重复调用添加。
.参数 json, eCharts_JSON类, 参考, 如果使用其他方式生成的json请先使用模块内置json类.解析()
.参数 图形索引, 整数型, , 索引从0开始。
.参数 区域起点类型, 整数型, , 0：自定义坐标；1：最小min；2：最大max；3：平均average；4：以x轴为基准；5：以y轴为基准
.参数 区域起点参数1, 文本型, 可空, 类型1、2、3时无效；类型为0或4时，必须与x轴上的数据对应
.参数 区域起点参数2, 双精度小数型, 可空, 类型1、2、3时无效；类型为0或5时，为y轴数值（可自由指定）
.参数 区域终点类型, 整数型, , 0：自定义坐标；1：最小min；2：最大max；3：平均average；4：以x轴为基准；5：以y轴为基准。（注意标注区域以x轴或y轴为基准时，终点类型也必须与起点对应相同，例如起点类型为4，终点类型也必须为4）
.参数 区域终点参数1, 文本型, 可空, 类型1、2、3时无效；类型为0或4时，必须与x轴上的数据对应
.参数 区域终点参数2, 双精度小数型, 可空, 类型1、2、3时无效；类型为0或5时，为y轴数值（可自由指定）
.局部变量 索引, 整数型
.局部变量 类型, 文本型

索引 ＝ json.数组_加入成员 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data”, “[]”) － 1
.如果真 (区域起点类型 ≥ 0 且 区域起点类型 ≤ 5 且 区域终点类型 ≥ 0 且 区域终点类型 ≤ 5)
    .判断开始 (区域起点类型 ＝ 0)  ' 自定义坐标
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][0].coord[0]”, , 区域起点参数1)
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][0].coord[1]”, , , 区域起点参数2)
    .判断 (区域起点类型 ＝ 1)  ' 最小值
        类型 ＝ “min”
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][0].type”, , 类型)
    .判断 (区域起点类型 ＝ 2)  ' 最大值
        类型 ＝ “max”
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][0].type”, , 类型)
    .判断 (区域起点类型 ＝ 3)  ' 平均值
        类型 ＝ “average”
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][0].type”, , 类型)
    .判断 (区域起点类型 ＝ 4)  ' x轴基准
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][0].xAxis”, , 区域起点参数1)
    .判断 (区域起点类型 ＝ 5)  ' y轴基准
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][0].yAxis”, , , 区域起点参数2)
    .默认
        返回 ()
    .判断结束

    .判断开始 (区域终点类型 ＝ 0)  ' 自定义坐标
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][1].coord[0]”, , 区域终点参数1)
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][1].coord[1]”, , , 区域终点参数2)
    .判断 (区域终点类型 ＝ 1)  ' 最小值
        类型 ＝ “min”
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][1].type”, , 类型)
    .判断 (区域终点类型 ＝ 2)  ' 最大值
        类型 ＝ “max”
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][1].type”, , 类型)
    .判断 (区域终点类型 ＝ 3)  ' 平均值
        类型 ＝ “average”
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][1].type”, , 类型)
    .判断 (区域终点类型 ＝ 4)  ' x轴基准
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][1].xAxis”, , 区域终点参数1)
    .判断 (区域终点类型 ＝ 5)  ' y轴基准
        json.置新值 (“series[” ＋ 到文本 (图形索引) ＋ “].markArea.data[” ＋ 到文本 (索引) ＋ “][1].yAxis”, , , 区域终点参数2)
    .默认
        返回 ()
    .判断结束

.如果真结束

